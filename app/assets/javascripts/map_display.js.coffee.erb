jQuery ->
  class LeafletMap
    @OpenCycle: 'https://tile.cyclestreets.net/opencyclemap/{z}/{x}/{y}@2x.png'
    @OSStreet: 'https://tile.cyclestreets.net/osopendata/{z}/{x}/{y}.png'
    @Mapnik: 'https://tile.cyclestreets.net/mapnik/{z}/{x}/{y}.png'
    @CyclestreetsKey: "<%= Geocoder::API_KEY %>"
    @CyclestreetsGeoUrl: "<%= Geocoder::URL %>"
    @CyclestreetsCollisionUrl: "<%= Geocoder::COLLISIONS_URL %>"

    constructor: (center, opts) ->
      @domId = opts.domid || 'map'
      @map = L.map(@domId)
      @setCenter(center)
      @geoInput = $("##{opts.geoInput}")
      @buildCollistionLayer() if opts.collisions?
      @buildIssueLayer(opts.issueurl) if opts.issueurl?
      @addLayers(opts.hidelayers)
      @addSearch() if opts.search?
      @addDraw() if opts.draw?
      @addStaticFeature(opts.feature) if opts.feature?
      @map.removeControl(@map.zoomControl) if opts.hidezoom?

    featurePointToLayer: (feature, latlng) =>
      icon = new L.Icon.Default(iconUrl: feature.properties.thumbnail)
      L.marker(latlng, {icon: icon}).addTo @map

    addStaticFeature: (collection) ->
      L.geoJson(collection, {
        style: {color: 'black'}, pointToLayer: @featurePointToLayer
      }).addTo @map
      return

    buildIssueLayer: (url) ->
      @issueLayer = L.geoJson(null, {
        pointToLayer: @featurePointToLayer.bind(@)
        fillOpacity: 0.1
        stroke: 'black'
        onEachFeature: (feature, layer) ->
          img = "<img src='#{feature.properties.image_url}' width='37' height='37'>" if feature.properties.image_url
          layer.bindPopup( "#{if img then img else ''}
           <h3><a href='#{feature.properties.url}'> #{feature.properties.title} </a></h3>
           <p>created by <a href='#{feature.properties.created_by_url}'> #{feature.properties.created_by} </p>"
          )
      }).addTo(@map)
      @issueJSONLayer = new L.LayerJSON({
        url: "#{url}?bbox={lon1},{lat1},{lon2},{lat2}"
        propertyItems: 'features'
        propertyLoc: 'geometry.coordinates'
        updateOutBounds: false
        hashGenerator: (data) ->
          data.id
        dataToMarker: @issueLayer.addData.bind(@issueLayer)
      }).addTo(@map)

    setCenter: (center) ->
      @map.fitBounds(center.fitBounds) if center.fitBounds?
      @map.setView(center.latLon, center.zoom) if center.latLon
      return

    addLayers: (hidelayers) ->
      openCycle = L.tileLayer(@constructor.OpenCycle, opacity: 0.8)
      openCycle.addTo(@map)
      baseLayers = {
        "OpenCycleMap":  openCycle,
        "OS StreetView": L.tileLayer(@constructor.OSStreet, opacity: 0.8),
        "OpenStreetMap": L.tileLayer(@constructor.Mapnik, opacity: 0.8),
      }
      additionalLayer = if @collisionLayer
        {Collisions: @collisionLayer}
      else if @issueJSONLayer?
        {Issues: @issueJSONLayer}

      L.control.layers(baseLayers, additionalLayer).addTo(@map) unless hidelayers

    search: (text, callback) =>
      bbox = @map.getBounds().toBBoxString()
      params = [
        {name: 'q', value: text}
        {name: 'key', value: @constructor.CyclestreetsKey}
        {name: 'bbox', value: bbox},
        {name: 'geometries', value: 1}
      ]
      $.ajax(
        url: @constructor.CyclestreetsGeoUrl
        data: params
        dataType: 'jsonp'
        timeout: 10000
        success: callback
      )

    buildCollistionLayer: =>
      lookup = {
        fatal: { color: '#aa0000', fillColor: '#ff0000', radius: 10 },
        serious: { color: '#e44500', fillColor: '#ff8814', radius: 8 },
        slight: { color: '#a7932f', fillColor: '#fcff00', radius: 6 }
      }
      params = [
        {name: 'key', value: @constructor.CyclestreetsKey}
        {name: 'fields', value: 'id,latitude,longitude,datetime,severity,url'},
        {name: 'datetime', value: 'friendly'},
      ]
      @collisionLayer = new L.LayerJSON({
        url: "#{@constructor.CyclestreetsCollisionUrl}?#{$.param(params)}&bbox={lon1},{lat1},{lon2},{lat2}"
        propertyItems: 'features'
        propertyLoc: ['properties.latitude','properties.longitude'],
        dataToMarker: (feature, latlng) =>
          props = lookup[feature.properties.severity]
          marker = new L.CircleMarker(latlng, props).addTo @collisionLayer
          marker.bindPopup(
            "<h3><a href=\"#{feature.properties.url}\">Collision #{feature.properties.id}</a></h3>
            <p>Date and time: #{feature.properties.datetime} </p>
            <p>Severity: #{feature.properties.severity} </p>
            <p><a href=\"#{feature.properties.url}\">View on CycleStreets</a></p>"
          )
      })

    formatJSON: (rawjson) ->
      json = {}
      featureName = (props) ->
        "#{props.name} (#{props.near})"

      # Hacked Leaflet.search for it to work with features but potential solution is here:
      # https://github.com/stefanocudini/leaflet-search/pull/111#issuecomment-210533909
      for feature in rawjson.features
        json[featureName(feature.properties)] = L.geoJson(type: "FeatureCollection", features: [feature])
      json

    addSearch: =>
      @map.addControl(
        new (L.Control.Search)(
          autoCollapse: true
          sourceData: @search
          formatData: @formatJSON
          markerLocation: false
          autoType: false
          minLength: 2)
      )

    addDraw: =>
      drawnItems = new L.FeatureGroup()
      @map.addLayer drawnItems
      drawControl = new L.Control.Draw {
        position: 'topright'
        edit: {
          featureGroup: drawnItems
        }
        draw: {
          circle: false
          rectangle: false
        }
      }

      @map.addControl drawControl
      @map.on 'draw:created', (e) =>
        layer = e.layer
        drawnItems.addLayer layer
        @geoInput.val JSON.stringify(layer.toGeoJSON())
        $('.tabs').children('li.area, li.route, li.point').css({opacity: 0.3}).prop('disabled', true)
        return

      @map.on 'draw:drawstart', (e) =>
        $('.icon-undo').css({opacity: 1})

      @map.on 'draw:drawstop', (e) =>
        $('.icon-undo').css({opacity: 0.3})

      # Bind our icons to the Leaflet draw icons
      lDrawMapping = {
        area: "draw-polygon"
        route: "draw-polyline"
        point: "draw-marker"
      }

      for own localN,lDrawName of lDrawMapping
        $("li.#{localN} a").click (evt) ->
          target = evt.target
          if $(target).parent().parent().prop('disabled')
            false
          else
            $(".leaflet-draw-#{lDrawMapping[target.className]}")[0].click()

      $('.edit-clear').click ->
        drawnItems.clearLayers()
        $('.tabs').children('li.area, li.route, li.point').css({opacity: 1}).prop('disabled', false)
        @geoInput?.val(null)

      $('.edit-undo').click ->
        $('ul.leaflet-draw-actions a[title^="Delete"]')[0]?.click()

  for domMap in $('.map-data')
    new LeafletMap($(domMap).data('center'), $(domMap).data('opts'))
